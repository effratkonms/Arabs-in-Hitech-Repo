DEFINE
	MEASURE 'Manual Selected Population'[Selected Manual Population] = SELECTEDVALUE('Manual Selected Population'[Population])
	MEASURE 'Fact Bagrut Denom'[Total 12th Grade Students] = VAR selectedManualPopulation = [Selected Manual Population]
		RETURN
			SWITCH(
				selectedManualPopulation,
				"החברה הערבית", CALCULATE(
					SUM('Fact Bagrut Denom'[12th Grade Students]),
					FILTER(
						ALL(Population),
						Population[Population] = "Arab"
					)
				),
				SUM('Fact Bagrut Denom'[12th Grade Students])
			)
	MEASURE 'Fact Bagrut Qualifiers'[Total Special Bagrut] = // SUM('Fact Bagrut Qualifiers'[Value])
		VAR selectedManualPopulation = [Selected Manual Population]
		RETURN
			SWITCH(
				selectedManualPopulation,
				"החברה הערבית", CALCULATE(
					SUM('Fact Bagrut Qualifiers'[Value]),
					FILTER(
						ALL(Population),
						Population[Population] = "Arab"
					)
				),
				SUM('Fact Bagrut Qualifiers'[Value])
			)
	MEASURE 'Fact Bagrut Qualifiers'[Total Bagrut w Honors] = CALCULATE(
			[Total Special Bagrut],
			'Fact Bagrut Qualifiers'[Attribute] = "בגרות בהצטיינות"
		)
	MEASURE 'Key Metric Bagrut Template'[Key Bagrut Metric #] = VAR metric = SELECTEDVALUE('Key Metric Bagrut Template'[Metric])
		VAR selectedSubject = SELECTEDVALUE('Key Metric Bagrut Template'[Subject])
		RETURN
			SWITCH(
				metric,
				"12th Grade Students", [Total 12th Grade Students],
				"Eligible w/ Honors", [Total Bagrut w Honors],
				"Eligible for 5 Units", CALCULATE(
					[Total Special Bagrut],
					KEEPFILTERS('Fact Bagrut Qualifiers'[Attribute] = selectedSubject)
				)
			)

EVALUATE
	SUMMARIZECOLUMNS(
		"Selected Manual Population", 'Manual Selected Population'[Selected Manual Population],
		"Total 12th Grade Students", 'Fact Bagrut Denom'[Total 12th Grade Students],
		"Total Special Bagrut", 'Fact Bagrut Qualifiers'[Total Special Bagrut],
		"Total Bagrut w Honors", 'Fact Bagrut Qualifiers'[Total Bagrut w Honors],
		"Key Bagrut Metric #", 'Key Metric Bagrut Template'[Key Bagrut Metric #]
	)